plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.23.3'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'tv.galaxe'
version = '2.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public" }
    maven { url = "https://repo.mattstudios.me/artifactory/public/" }
}

dependencies {
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: '1.20.4-R0.1-SNAPSHOT'
    implementation group: 'dev.triumphteam', name: 'triumph-gui', version: '3.1.7'
    compileOnly group: 'net.luckperms', name: 'api', version: '5.4'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.withType(Jar) {
    if (project.properties.containsKey("workspace")) {
        destinationDir = file("$rootDir/.papermc/plugins")
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

spotless {
    ratchetFrom 'origin/main'

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    java {
        importOrder()
        removeUnusedImports()
        cleanthat()
        eclipse()
        formatAnnotations()
    }
}
